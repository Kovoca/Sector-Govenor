/// @self Asset.GMObject.obj_ncombat
function necron_tomb_raid_post_battle_sequence() {
    if (!string_count("wake", battle_special)) {
        if (defeat == 1) {
            obj_controller.combat = 0;
            obj_controller.cooldown = 10;
            obj_turn_end.alarm[1] = 4;
        } else if (defeat == 0) {
            battle_data.mission_stage += 1;
            obj_controller.combat = 0;
            var pip = instance_create(0, 0, obj_popup);
            pip.pop_data = battle_data;

            with (pip) {
                necron_tomb_mission_start();
                necron_tomb_mission_sequence();
                number = pop_data.number;
            }
        }
    } else {
        var pip = instance_create(0, 0, obj_popup);
        with (pip) {
            title = "Necron Tomb Awakens";
            image = "necron_army";
            if (obj_ncombat.defeat == 0) {
                text = "Your marines make a tactical retreat back to the surface, hounded by Necrons all the way.  The Inquisition mission is a failure- you were to blow up the Necron Tomb World stealthily, not wake it up.  The Inquisition is not pleased with your conduct.";
            } else {
                text = "Your marines are killed down to the last man.  The Inquisition mission is a failure- you were to blow up the Necron Tomb World stealthily, not wake it up.  The Inquisition is not pleased with your conduct.";
            }
        }

        var _star_obj = find_star_by_name(battle_loc);
        if (_star_obj != noone) {
            with (_star_obj) {
                var planet = obj_ncombat.battle_id;
                if (remove_planet_problem(planet, "necron")) {
                    p_necrons[planet] = 4;
                }
                if (awake_tomb_world(p_feature[planet]) == 0) {
                    awaken_tomb_world(p_feature[planet]);
                }
            }
        }

        pip.pop_data = battle_data;

        alter_disposition(eFACTION.INQUISITION, -5);
        obj_controller.combat = 0;

        with (pip) {
            number = pop_data.number;
        }
    }
}

/// @self Asset.GMObject.obj_ncombat
function protect_raiders_battle_aftermath() {
    instance_activate_object(obj_star);
    // show_message(obj_turn_end.current_battle);
    // show_message(obj_turn_end.battle_world[obj_turn_end.current_battle]);
    // title / text / image / speshul
    var cur_star = battle_object;
    var planet = battle_id;
    var _planet = cur_star.get_planet_data(planet);
    var _planet_string = _planet.name();
    _planet.remove_problem("protect_raiders");
    if (!defeat) {
        _planet.add_disposition(15);
        var tixt = $"The Raiding forces on {_planet_string} have been removed.  The citizens and craftsman may sleep more soundly. (planet disp +15)";

        scr_popup("Planet Protected", tixt, "protect_raiders", "");
        scr_event_log("", $"Governor Request completed: Raiding forces on {_planet_string} have been eliminated.", cur_star.name);
    } else {
        _planet.add_disposition(-15);
        var tixt = $"The Raiding forces on {_planet_string} dispatched with your forces and will continue with their bloody practices.  The citizens remain unsafe and the governor is unimpressed. (planet disp -15)";
        scr_popup("Planet Protected", tixt, "protect_raiders", "");

        scr_event_log("", $"Governor Request failed: Raiding forces on {_planet_string} continue to harrass population.", cur_star.name);
    }
    instance_deactivate_object(obj_star);
}

/// @self Asset.GMObject.obj_ncombat
function hunt_fallen_battle_aftermath() {
    if (!defeat) {
        with (obj_turn_end) {
            remove_planet_problem(battle_world[current_battle], "fallen", battle_object[current_battle]);
            var tixt = "The Fallen on " + battle_object[current_battle].name;
            tixt += scr_roman(battle_world[current_battle]);
            scr_event_log("", $"Mission Succesful: {tixt} have been captured or purged.");
            tixt += $" have been captured or purged.  They shall be brought to the Chapter {obj_ini.role[100][14]}s posthaste, in order to account for their sins.  ";
            var _tex_options = [
                "Suffering is the beginning to penance.",
                "Their screams shall be the harbringer of their contrition.",
                "The shame they inflicted upon us shall be written in their flesh."
            ];
            tixt += _tex_options[choose(0, 0, 1, 2)];
            scr_popup("Hunt the Fallen Completed", tixt, "fallen", "");
        }
    }
}

function space_hulk_explore_battle_aftermath() {
    if (!defeat && hulk_treasure > 0) {
        var shi = 0, loc = "";

        var shiyp = instance_nearest(battle_object.x, battle_object.y, obj_p_fleet);
        if (shiyp.x == battle_object.x && shiyp.y == battle_object.y) {
            shi = fleet_full_ship_array(shiyp)[0];
            loc = obj_ini.ship[shi];
        }

        if (hulk_treasure == 1) {
            // Requisition
            var _reqi = irandom_range(30, 60) * 10;
            obj_controller.requisition += _reqi;

            var pop = instance_create(0, 0, obj_popup);
            pop.image = "space_hulk_done";
            pop.title = "Space Hulk: Resources";
            pop.text = $"Your battle brothers have located several luxury goods and coginators within the Space Hulk.  They are salvaged and returned to the ship, granting {_reqi} Requisition.";
        } else if (hulk_treasure == 2) {
            // Artifact
            //TODO this will eeroniously put artifacts in the wrong place but will resolve crashes
            var last_artifact = scr_add_artifact("random", "random", 4, loc, shi + 500);
            var i = 0;

            var pop = instance_create(0, 0, obj_popup);
            pop.image = "space_hulk_done";
            pop.title = "Space Hulk: Artifact";
            pop.text = $"An Artifact has been retrieved from the Space Hulk and stowed upon {loc}.  It appears to be a {obj_ini.artifact[last_artifact]} but should be brought home and identified posthaste.";
            scr_event_log("", "Artifact recovered from the Space Hulk.");
        } else if (hulk_treasure == 3) {
            // STC
            scr_add_stc_fragment(); // STC here
            var pop;
            pop = instance_create(0, 0, obj_popup);
            pop.image = "space_hulk_done";
            pop.title = "Space Hulk: STC Fragment";
            pop.text = "An STC Fragment has been retrieved from the Space Hulk and safely stowed away.  It is ready to be decrypted or gifted at your convenience.";
            scr_event_log("", "STC Fragment recovered from the Space Hulk.");
        } else if (hulk_treasure == 4) {
            // Termie Armour
            var termi = choose(2, 2, 2, 3);
            scr_add_item("Terminator Armour", termi);
            var pop;
            pop = instance_create(0, 0, obj_popup);
            pop.image = "space_hulk_done";
            pop.title = "Space Hulk: Terminator Armour";
            pop.text = "The fallen heretics wore several suits of Terminator Armour- a handful of them were found to be cleansible and worthy of use.  " + string(termi) + " Terminator Armour has been added to the Armamentarium.";
        }
    }

    // Hulk fully cleared this battle: offer the salvage choice. The hulk is removed from the map
    // by whichever option the player picks (space_hulk_strip / space_hulk_surrender).
    if (!defeat && hulk_cleared) {
        var _shi = 0, _loc = "";
        var _shiyp = instance_nearest(battle_object.x, battle_object.y, obj_p_fleet);
        if (_shiyp.x == battle_object.x && _shiyp.y == battle_object.y) {
            _shi = fleet_full_ship_array(_shiyp)[0];
            _loc = obj_ini.ship[_shi];
        }

        var pop = instance_create(0, 0, obj_popup);
        pop.image = "space_hulk_done";
        pop.title = "Space Hulk Cleared";
        pop.text = "The last of the hulk's defenders are purged and the drifting wreck falls silent. It is yours to dispose of. Strip it for the Chapter, or tow it to the nearest Forge World for the Adeptus Mechanicus?";
        pop.hulk_star = battle_object;
        pop.hulk_loot_ship = _shi;
        pop.hulk_loot_loc = _loc;
        with (pop) {
            replace_options([
                { str1: "Strip it for the Chapter", choice_func: space_hulk_strip },
                { str1: "Tow it to the nearest Forge World", choice_func: space_hulk_surrender }
            ]);
        }
    }
}

/// @description Salvage choice: strip a cleared space hulk for the Chapter. Runs in obj_popup scope.
/// Raises Inquisitorial suspicion and angers the Mechanicus, but yields requisition and a relic
/// that carries the normal chance of chaos/daemonic taint. Removes the hulk from the map.
function space_hulk_strip() {
    // --- Penalties (tunable) ---
    // Inquisition takes note; the Mechanicus consider the claim theft from the Omnissiah.
    obj_ini.chapter_data.chapter_suspicion = clamp(obj_ini.chapter_data.chapter_suspicion + 1, -5, 5);
    obj_controller.disposition[eFACTION.MECHANICUS] = clamp(obj_controller.disposition[eFACTION.MECHANICUS] - 12, 0, 100);
    // The uncancellable cost of hoarding forbidden discoveries: Chapter standing drops a tier
    // (100 -> 75 leaves the >=85 "Loyal" band). Both visible and hidden loyalty fall, the same
    // idiom the existing crime penalties use, so it persists through Inquisition inspections.
    // Gifting any recovered STC to the Mechanicus can repair their disposition, never this.
    var _loyalty_hit = 25;
    obj_controller.loyalty = clamp(obj_controller.loyalty - _loyalty_hit, 0, 100);
    obj_controller.loyalty_hidden = clamp(obj_controller.loyalty_hidden - _loyalty_hit, 0, 100);

    // --- Rewards (tunable) ---
    var _reqi = irandom_range(80, 120) * 10; // ~1000 Requisition
    obj_controller.requisition += _reqi;

    // 1-3 relics, most carrying chaos/daemonic taint. ~65% are forced daemonic; the rest still
    // roll the normal ~30% taint chance, so the overall haul is dangerous more often than not.
    var _arts = irandom_range(1, 3);
    for (var _a = 0; _a < _arts; _a++) {
        var _art_tags = (random(1) < 0.65) ? "daemonic" : "random";
        scr_add_artifact("random", _art_tags, 4, hulk_loot_loc, hulk_loot_ship + 500);
    }

    // Rare STC fragment in the haul. Kept low on purpose: a guaranteed STC would let the player
    // gift it back to the Mechanicus and buy back the disposition they just lost (the loyalty
    // hit above is what they can never undo).
    var _found_stc = (random(1) < 0.20);
    if (_found_stc) {
        scr_add_stc_fragment();
    }

    if (instance_exists(hulk_star)) {
        with (hulk_star) {
            instance_destroy();
        }
    }
    if (instance_exists(obj_star_select)) {
        with (obj_star_select) {
            instance_destroy();
        }
    }
    obj_controller.sel_system_x = 0;
    obj_controller.sel_system_y = 0;

    image = "space_hulk_done";
    title = "Space Hulk Stripped";
    var _stc_line = _found_stc ? " An intact STC fragment is prised from the wreck and spirited away." : "";
    text = $"Your Chapter strips the hulk for itself. {_arts} relic(s) and {_reqi} Requisition worth of archeotech are hauled aboard.{_stc_line} The unsanctioned claim will not go unnoticed: the Adeptus Mechanicus consider it theft from the Omnissiah, the Inquisition's gaze lingers, and the Chapter's standing suffers for hoarding forbidden discoveries.";
    reset_popup_options();
    cooldown = 20;
}

/// @description Salvage choice: tow a cleared space hulk to the nearest Forge World. Runs in
/// obj_popup scope. The sanctioned, expected course: the Mechanicus are grateful and send a
/// token of thanks. Removes the hulk from the map.
function space_hulk_surrender() {
    // --- Rewards (tunable): the payoff here is Mechanicus favour, not loot. ---
    obj_controller.disposition[eFACTION.MECHANICUS] = clamp(obj_controller.disposition[eFACTION.MECHANICUS] + 12, 0, 100);

    var _reqi = irandom_range(8, 12) * 10; // ~100 Requisition, a token of thanks
    obj_controller.requisition += _reqi;

    // A single minor, untainted relic of the sort the Mechanicus part with in trade. No major
    // relic wargear here: the Mechanicus are stingy and keep their best for themselves.
    scr_add_artifact("random_nodemon", "minor", 4, hulk_loot_loc, hulk_loot_ship + 500);

    if (instance_exists(hulk_star)) {
        with (hulk_star) {
            instance_destroy();
        }
    }
    if (instance_exists(obj_star_select)) {
        with (obj_star_select) {
            instance_destroy();
        }
    }
    obj_controller.sel_system_x = 0;
    obj_controller.sel_system_y = 0;

    image = "space_hulk_done";
    title = "Space Hulk Surrendered";
    text = $"The hulk is towed to the nearest Forge World for the Adeptus Mechanicus to pick apart at their leisure. They receive it gratefully, their disposition toward your Chapter warms, and they send a token of thanks worth {_reqi} Requisition along with a minor relic from their archives.";
    reset_popup_options();
    cooldown = 20;
}
