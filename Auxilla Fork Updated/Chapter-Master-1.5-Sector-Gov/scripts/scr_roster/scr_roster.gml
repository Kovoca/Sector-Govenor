function Roster() constructor {
    full_roster_units = [];
    selected_units = [];
    full_roster = {};
    selected_roster = {};
    /// @type {Array<Struct.ToggleButton>}
    ships = [];
    roster_location = "";
    roster_planet = 0;
    roster_string = "";
    /// @type {Array<Struct.ToggleButton>}
    squad_buttons = [];
    /// @type {Array<Struct.ToggleButton>}
    company_buttons = [];
    roster_local_string = "";
    local_button = new ToggleButton({str1: "Local Forces", text_halign: fa_center, text_color: CM_GREEN_COLOR, button_color: CM_GREEN_COLOR, active: false});

    select_all_ships = new UnitButtonObject({x1: 700, y1: 299, label: "All Ships", text_color: CM_GREEN_COLOR, button_color: CM_GREEN_COLOR});

    static only_locals = function() {
        for (var i = 0; i < array_length(ships); i++) {
            var _button = ships[i];
            _button.active = false;
        }
        local_button.active = true;
    };

    static format_roster_string = function() {
        roster_string = "";
        var _roster_types = struct_get_names(selected_roster);
        for (var i = 0; i < array_length(_roster_types); i++) {
            var _roster_type_name = _roster_types[i];
            var _roster_type_count = selected_roster[$ _roster_type_name];

            roster_string += $"{string_plural_count(_roster_type_name, _roster_type_count)}";
            roster_string += smart_delimeter_sign(_roster_types, i, false);
        }
    };

    static add_role_to_roster = function(role) {
        if (struct_exists(full_roster, role)) {
            full_roster[$ role]++;
        } else {
            full_roster[$ role] = 1;
        }
    };

    static add_role_to_selected_roster = function(role) {
        if (struct_exists(selected_roster, role)) {
            selected_roster[$ role]++;
        } else {
            selected_roster[$ role] = 1;
        }
    };

    static is_roster_unit_local = function(unit) {
        if (is_struct(unit)) {
            return unit.ship_location == -1;
        } else {
            return obj_ini.veh_lid[unit[0]][unit[1]];
        }
    };

    static update_roster = function() {
        selected_roster = {};
        var _allow_dreadnoughts = false;
        for (var i = 0; i < array_length(selected_units); i++) {
            array_push(full_roster_units, selected_units[i]);
        }
        selected_units = [];
        var _valid_ship = [];
        for (var i = 0; i < array_length(ships); i++) {
            if (ships[i].active) {
                array_push(_valid_ship, ships[i].ship_id);
            }
        }
        var _valid_squad_types = [];
        for (var i = 0; i < array_length(squad_buttons); i++) {
            if (squad_buttons[i].active) {
                array_push(_valid_squad_types, squad_buttons[i].squad);
                if (squad_buttons[i].squad == "dreadnought") {
                    _allow_dreadnoughts = true;
                }
            }
        }
        var _valid_vehicles = [];
        for (var i = 0; i < array_length(vehicle_buttons); i++) {
            if (vehicle_buttons[i].active) {
                array_push(_valid_vehicles, vehicle_buttons[i].vehic_id);
            }
        }

        var _valid_companies = [];
        for (var i = 0; i < array_length(company_buttons); i++) {
            if (company_buttons[i].active) {
                array_push(_valid_companies, company_buttons[i].company_id);
            }
        }
        for (var i = array_length(full_roster_units) - 1; i >= 0; i--) {
            var _add = false;
            var _unit = full_roster_units[i];
            var _valid_type = true;
            var is_unit = is_struct(_unit);
            if (is_unit) {
                if (!array_contains(_valid_companies, _unit.company)) {
                    continue;
                }
                if (_unit.squad_type() != "none") {
                    _valid_type = array_contains(_valid_squad_types, _unit.squad_type());
                } else {
                    var _armour_data = _unit.get_armour_data();
                    if (is_struct(_armour_data)) {
                        if (_armour_data.has_tag("dreadnought")) {
                            _valid_type = _allow_dreadnoughts;
                        }
                    }
                    // Guardsmen answer to their own filter button rather than always passing
                    var _grd_role = _unit.role();
                    if (_grd_role == "Guardsman" || _grd_role == "Guard Squad" || _grd_role == "Guard Sergeant" || _grd_role == "Veteran Guard" || _grd_role == "Heavy Weapons Team") {
                        _valid_type = array_contains(_valid_squad_types, "guardsman");
                    }
                }

                if (_unit.ship_location > -1) {
                    if (array_contains(_valid_ship, _unit.ship_location) && _valid_type) {
                        _add = true;
                    }
                } else if (local_button.active && _valid_type) {
                    _add = true;
                }
            } else {
                if (!array_contains(_valid_companies, _unit[0])) {
                    continue;
                }
                var _role = obj_ini.veh_role[_unit[0]][_unit[1]];
                var _vehic_lid = obj_ini.veh_lid[_unit[0]][_unit[1]];
                if (array_contains(_valid_vehicles, _role)) {
                    if (_vehic_lid > -1) {
                        if (array_contains(_valid_ship, _vehic_lid)) {
                            _add = true;
                        }
                    } else if (local_button.active) {
                        _add = true;
                    }
                }
            }

            if (_add) {
                array_push(selected_units, _unit);
                array_delete(full_roster_units, i, 1);
                if (is_unit) {
                    add_role_to_selected_roster(_unit.role());
                } else {
                    add_role_to_selected_roster(obj_ini.veh_role[_unit[0]][_unit[1]]);
                }
            }
        }

        format_roster_string();
    };

    static selected_count = function(){
        return array_length(selected_units);
    }

    static new_squad_button = function(display, squad_id) {
        var _button = new ToggleButton();
        display = string_replace(display, " Squad", "");
        if (display != "Command" && display != "Guardsmen") {
            display = string_plural(display);
        }
        _button.str1 = display;
        _button.text_halign = fa_center;
        _button.text_color = CM_GREEN_COLOR;
        _button.button_color = CM_GREEN_COLOR;
        _button.w = string_width(display) + 10;
        _button.active = true;
        _button.squad = squad_id;
        array_push(squad_buttons, _button);
    };

    ship_multi_selector = new MultiSelect([], "", {is_horizontal: false, max_height: 160});

    static new_ship_button = function(display, ship_id) {
        var _button = new ToggleButton();
        _button.update({str1: display, text_halign: fa_center, text_color: CM_GREEN_COLOR, button_color: CM_GREEN_COLOR, width: string_width(display) + 10, active: false, ship_id});

        _button.roster = self;
        _button.hover_func = method(_button, function() {
            roster.update_local_string(ship_id);
        });

        array_push(ships, _button);
        array_push(ship_multi_selector.toggles, _button);
    };

    static update_local_string = function(ship_id) {
        var selected_local_roster = {};
        var possible_local_roster = {};
        for (var i = 0; i < array_length(selected_units); i++) {
            var _unit = selected_units[i];
            var _ship_loc = (is_struct(_unit)) ? _unit.ship_location : obj_ini.veh_lid[_unit[0]][_unit[1]];
            if (_ship_loc == ship_id) {
                var _role = (is_struct(_unit)) ? _unit.role() : obj_ini.veh_role[_unit[0]][_unit[1]];
                if (_role == "") { continue; } // an empty role is an illegal struct key (crash); skip placeholder units
                if (struct_exists(selected_local_roster, _role)) {
                    selected_local_roster[$ _role]++;
                } else {
                    selected_local_roster[$ _role] = 1;
                }
            }
        }
        for (var i = 0; i < array_length(full_roster_units); i++) {
            var _unit = full_roster_units[i];
            var _ship_loc = (is_struct(_unit)) ? _unit.ship_location : obj_ini.veh_lid[_unit[0]][_unit[1]];
            if (_ship_loc == ship_id) {
                var _role = (is_struct(_unit)) ? _unit.role() : obj_ini.veh_role[_unit[0]][_unit[1]];
                if (_role == "") { continue; }
                if (struct_exists(possible_local_roster, _role)) {
                    possible_local_roster[$ _role]++;
                } else {
                    possible_local_roster[$ _role] = 1;
                }
            }
        }
        roster_local_string = "Selected\n";
        var _roster_types = struct_get_names(selected_local_roster);
        for (var i = 0; i < array_length(_roster_types); i++) {
            var _roster_type_name = _roster_types[i];
            var _roster_type_count = selected_local_roster[$ _roster_type_name];

            roster_local_string += $"{string_plural_count(_roster_type_name, _roster_type_count)}";
            roster_local_string += smart_delimeter_sign(_roster_types, i, false);
        }

        roster_local_string += "\n";
        roster_local_string += "Remaining\n";
        _roster_types = struct_get_names(possible_local_roster);
        for (var i = 0; i < array_length(_roster_types); i++) {
            var _roster_type_name = _roster_types[i];
            var _roster_type_count = possible_local_roster[$ _roster_type_name];

            roster_local_string += $"{string_plural_count(_roster_type_name, _roster_type_count)}";
            roster_local_string += smart_delimeter_sign(_roster_types, i, false);
        }
    };

    vehicle_buttons = [];

    static new_vehicle_button = function(display, vehicle_type) {
        var _button = new ToggleButton();
        _button.str1 = display;
        _button.text_halign = fa_center;
        _button.text_color = CM_GREEN_COLOR;
        _button.button_color = CM_GREEN_COLOR;
        _button.w = string_width(display) + 10;
        _button.active = true;
        _button.vehic_id = vehicle_type;
        array_push(vehicle_buttons, _button);
    };

    static determine_full_roster = function() {
        var _squads = [];
        var _vehicles = [];
        var _company_present = false;
        for (var co = 0; co <= obj_ini.companies; co++) {
            _company_present = false;
            for (var i = 0; i < array_length(obj_ini.role[co]); i++) {
                var _allow = false;
                var _unit = fetch_unit([co, i]);
                if (_unit.name() == "" || _unit.role() == "") {
                    continue;
                }
                if (_unit.hp() <= 0 || _unit.in_jail()) {
                    continue;
                }
                if (_unit.is_at_location(roster_location)) {
                    _allow = true;
                    if (_unit.planet_location > 0) {
                        _allow = _unit.planet_location == roster_planet;
                    }
                }
                if (_allow) {
                    _company_present = true;
                    array_push(full_roster_units, _unit);
                    add_role_to_roster(_unit.role());
                    if (_unit.squad != "none") {
                        var _squad_type = _unit.squad_type();
                        if (_squad_type != "none") {
                            if (!array_contains(_squads, _squad_type)) {
                                array_push(_squads, _squad_type);
                                var _squad = fetch_squad(_unit.squad);
                                new_squad_button(_squad.display_name, _squad_type);
                            }
                        }
                    } else {
                        if (!array_contains(_squads, "dreadnought")) {
                            if (_unit.is_dreadnought()) {
                                array_push(_squads, "dreadnought");
                                new_squad_button("Dreadnought", "dreadnought");
                            }
                        }
                        // Guardsmen and Guard Squads have no squad type, so give them their
                        // own filter button (added once) so they can be selected on their own.
                        var _grd_role = _unit.role();
                        if ((_grd_role == "Guardsman" || _grd_role == "Guard Squad" || _grd_role == "Guard Sergeant" || _grd_role == "Veteran Guard" || _grd_role == "Heavy Weapons Team") && !array_contains(_squads, "guardsman")) {
                            array_push(_squads, "guardsman");
                            new_squad_button("Guardsmen", "guardsman");
                        }
                    }
                }
            }

            var _raid_allowable = ["Land Speeder"];
            for (var i = 0; i < array_length(obj_ini.veh_race[co]); i++) {
                var _allow = false;
                if (obj_ini.veh_race[co][i] == 0) {
                    continue;
                }
                var _v_role = obj_ini.veh_role[co][i];
                if (obj_ini.veh_loc[co][i] == roster_location) {
                    if (obj_ini.veh_wid[co][i] > 0) {
                        if (obj_ini.veh_wid[co][i] == roster_planet) {
                            _allow = true;
                        }
                    }
                }
                if (obj_ini.veh_lid[co][i] > -1) {
                    if (obj_ini.veh_lid[co][i] >= array_length(obj_ini.ship_location)) {
                        obj_ini.veh_lid[co][i] = -1;
                    }
                    if (obj_ini.ship_location[obj_ini.veh_lid[co][i]] == roster_location) {
                        _allow = true;
                    }
                }
                if (_allow) {
                    if (instance_exists(obj_drop_select)) {
                        if (!obj_drop_select.attack) {
                            _allow = array_contains(_raid_allowable, _v_role);
                        }
                    }
                }
                if (_allow) {
                    _company_present = true;
                    array_push(full_roster_units, [co, i]);
                    if (!array_contains(_vehicles, obj_ini.veh_role[co][i])) {
                        array_push(_vehicles, obj_ini.veh_role[co][i]);
                        new_vehicle_button(obj_ini.veh_role[co][i], obj_ini.veh_role[co][i]);
                    }
                }
            }

            var _button = new ToggleButton();
            var _col = _company_present ? CM_GREEN_COLOR : c_red;
            var _display = co ? scr_roman_numerals()[co - 1] : "HQ";
            _button.str1 = _display;
            _button.text_halign = fa_center;
            _button.text_color = _col;
            _button.button_color = _col;
            _button.w = max(30, string_width(_display) + 10);
            _button.active = _company_present;
            _button.company_id = co;
            _button.company_present = _company_present;
            array_push(company_buttons, _button);
        }
        var _ships = get_player_ships(roster_location);
        var _ship_index;
        for (var s = 0; s < array_length(_ships); s++) {
            _ship_index = _ships[s];
            if (obj_ini.ship_carrying[_ship_index] > 0) {
                new_ship_button(obj_ini.ship[_ship_index], _ship_index);
                // Ship assault economy: a ship that has already supported its maximum
                // ground assaults this turn stays listed but locked and red, so the
                // player can see it is spent. The lock is enforced in
                // scr_drop_select_function (clicks and Select All are forced back off),
                // and update_roster only admits units whose ship toggle is active.
                // Applies to attacks only; raids and purges do not spend uses.
                if (instance_exists(obj_drop_select)) {
                    // Attack rosters gate on the ship's assault counter, raid rosters on
                    // its raid counter (each independent), so a ship spent on one action
                    // still shows available for the other.
                    var _dr_used = obj_drop_select.attack ? ship_assaults_used(_ship_index) : ship_raids_used(_ship_index);
                    if (_dr_used >= SHIP_ASSAULTS_PER_TURN) {
                        var _spent_btn = ships[array_length(ships) - 1];
                        _spent_btn.assault_locked = true;
                        _spent_btn.active = false;
                        _spent_btn.text_color = c_red;
                        _spent_btn.button_color = c_red;
                        _spent_btn.tooltip = obj_drop_select.attack ? "This ship has already supported the maximum number of ground assaults this turn." : "This ship has already supported the maximum number of raids this turn.";
                    }
                }
            }
        }
    };

    static add_to_battle = function() {
        var meeting = false;
        if (instance_exists(obj_temp_meeting)) {
            meeting = true;
            if ((company == 0) && (v <= obj_temp_meeting.dudes) && (obj_temp_meeting.present[v] == 1)) {
                okay = 1;
            } else if ((company > 0) || (v > obj_temp_meeting.dudes)) {
                okay = 0;
            }
        }
        var size_count = 0;
        var _limit = obj_ncombat.man_size_limit;
        var _has_limit = _limit > 0;
        for (var i = 0; i < array_length(selected_units); i++) {
            if (_has_limit && _limit == size_count) {
                break;
            }
            var _add = true;

            if (is_struct(selected_units[i])) {
                var _unit = selected_units[i];
                if (_has_limit) {
                    var _size = _unit.get_unit_size();
                    _add = (_size + size_count) <= _limit;
                    if (_add) {
                        size_count += _size;
                    }
                }
                if (_add) {
                    add_unit_to_battle(_unit, meeting, true);
                }
            } else {
                var _vehic = selected_units[i];
                var _type = obj_ini.veh_role[_vehic[0]][_vehic[1]];
                if (_has_limit) {
                    var _size = scr_unit_size("", _type, true);
                    _add = _size + size_count <= _limit;
                    if (_add) {
                        size_count += _size;
                    }
                }
                if (_add) {
                    add_vehicle_to_battle(_vehic[0], _vehic[1], is_roster_unit_local(_vehic));
                }
            }
        }
    };

    static marines_total = function() {
        var _marines = 0;
        for (var i = 0; i < array_length(full_roster_units); i++) {
            _marines += is_struct(full_roster_units[i]);
        }
        for (var i = 0; i < array_length(selected_units); i++) {
            _marines += is_struct(selected_units[i]);
        }
        return _marines;
    };

    static purge_bombard_score = function() {
        var _purge_score = 0;
        for (var i = 0; i < array_length(ships); i++) {
            if (ships[i].active) {
                var _id = ships[i].ship_id;
                var _class = player_ships_class(_id);
                if (obj_ini.ship_class[_id] == "Gloriana") {
                    _purge_score += 4;
                } else if (_class == "capital") {
                    _purge_score += 3;
                } else if (_class == "frigate") {
                    _purge_score += 1;
                }
            }
        }
        return _purge_score;
    };
}

function PurgeButton(purge_image, xx, yy, purge_type) constructor {
    x1 = xx;
    y1 = yy;
    x2 = 0;
    y2 = 0;
    width = 351;
    height = 63;
    active = 0;
    bright_shader = 0.8;
    self.purge_type = purge_type;
    self.purge_image = purge_image;
    description = "";

    static hover = function() {
        return scr_hit(x1, y1, x1 + width, y1 + height);
    };

    static draw = function() {
        if (active) {
            if (hover()) {
                bright_shader = min(1.2, bright_shader + 0.02);
            } else {
                bright_shader = max(0.8, bright_shader - 0.02);
            }
        } else {
            bright_shader = 0.35;
        }
        shader_set(light_dark_shader);
        shader_set_uniform_f(shader_get_uniform(light_dark_shader, "highlight"), bright_shader);
        scr_image("purge", purge_image, x1, y1, width, height);
        shader_reset();
        // The description field existed but was never rendered. Drawn after the
        // shader resets so the tooltip is not tinted; inactive buttons explain why.
        if ((description != "") && hover()) {
            var _tip = description;
            if (!active) {
                _tip += "\n\nYour selected force cannot perform this purge.";
            }
            tooltip_draw(_tip);
        }
    };

    static clicked = function() {
        if (active) {
            return point_and_click([x1, y1, x1 + width, y1 + height]);
        }
        return false;
    };
}

function setup_battle_formations() {
    // Formation here
    var new_combat = obj_ncombat;
    obj_controller.bat_devastator_column = obj_controller.bat_deva_for[new_combat.formation_set];
    obj_controller.bat_assault_column = obj_controller.bat_assa_for[new_combat.formation_set];
    obj_controller.bat_tactical_column = obj_controller.bat_tact_for[new_combat.formation_set];
    obj_controller.bat_veteran_column = obj_controller.bat_vete_for[new_combat.formation_set];
    obj_controller.bat_hire_column = obj_controller.bat_hire_for[new_combat.formation_set];
    obj_controller.bat_librarian_column = obj_controller.bat_libr_for[new_combat.formation_set];
    obj_controller.bat_command_column = obj_controller.bat_comm_for[new_combat.formation_set];
    obj_controller.bat_techmarine_column = obj_controller.bat_tech_for[new_combat.formation_set];
    obj_controller.bat_terminator_column = obj_controller.bat_term_for[new_combat.formation_set];
    obj_controller.bat_honor_column = obj_controller.bat_hono_for[new_combat.formation_set];
    obj_controller.bat_dreadnought_column = obj_controller.bat_drea_for[new_combat.formation_set];
    obj_controller.bat_rhino_column = obj_controller.bat_rhin_for[new_combat.formation_set];
    obj_controller.bat_predator_column = obj_controller.bat_pred_for[new_combat.formation_set];
    obj_controller.bat_landraider_column = obj_controller.bat_landraid_for[new_combat.formation_set];
    obj_controller.bat_landspeeder_column = obj_controller.bat_landspee_for[new_combat.formation_set];
    obj_controller.bat_whirlwind_column = obj_controller.bat_whirl_for[new_combat.formation_set];
    obj_controller.bat_scout_column = obj_controller.bat_scou_for[new_combat.formation_set];
}

function add_unit_to_battle(unit, meeting, is_local) {
    var new_combat = obj_ncombat;
    var man_size = 1;

    //Same as co/company and v, but with extra comprovations in case of a meeting (meeting?)
    var _role = obj_ini.role[100];
    var cooh = 0;
    var va = 0;
    var v = unit.marine_number;
    var company = unit.company;
    if (!meeting) {
        cooh = company;
        va = v;
    } else {
        if (v <= obj_temp_meeting.dudes) {
            cooh = obj_temp_meeting.company[v];
            va = obj_temp_meeting.ide[v];
        }
    }
    var _armour_data = unit.get_armour_data();
    var _wearing_armour = is_struct(_armour_data);

    var col = 0, targ = 0, moov = 0;
    var ftype = "";
    var _unit_role = unit.role();

    if (new_combat.battle_special == "space_hulk") {
        new_combat.player_starting_dudes++;
    }

    if (_unit_role == obj_ini.role[100][18]) {
        col = obj_controller.bat_tactical_column;
        ftype = "tactical"; //sergeants
        new_combat.sgts++;
    } else if (_unit_role == _role[19]) {
        col = obj_controller.bat_veteran_column;
        ftype = "veteran";
        new_combat.vet_sgts++;
    }
    if (_unit_role == _role[12]) {
        //scouts
        col = obj_controller.bat_scout_column;
        ftype = "scout";
        new_combat.scouts++;
    } else if (array_contains([obj_ini.role[100][8], $"{_role[15]} Aspirant", $"{_role[14]} Aspirant"], _unit_role)) {
        col = obj_controller.bat_tactical_column;
        ftype = "tactical"; //tactical_marines
        new_combat.tacticals++;
    } else if (_unit_role == _role[3]) {
        //veterans and veteran sergeants
        col = obj_controller.bat_veteran_column;
        ftype = "veteran";
        new_combat.veterans++;
    } else if (_unit_role == _role[9]) {
        //devastators
        col = obj_controller.bat_devastator_column;
        ftype = "devastator";
        new_combat.devastators++;
    } else if (_unit_role == _role[10]) {
        //assualt marines
        col = obj_controller.bat_assault_column;
        ftype = "assault";
        new_combat.assaults++;

        //librarium roles
    } else if (unit.IsSpecialist(SPECIALISTS_LIBRARIANS, true)) {
        col = obj_controller.bat_librarian_column;
        ftype = "librarian"; //librarium
        new_combat.librarians++;
        moov = 1;
    } else if (_unit_role == _role[16]) {
        //techmarines
        col = obj_controller.bat_techmarine_column;
        ftype = "techmarine";
        new_combat.techmarines++;
        moov = 2;
    } else if (_unit_role == _role[2]) {
        //honour guard
        col = obj_controller.bat_honor_column;
        ftype = "honor";
        new_combat.honors++;
    } else if (unit.IsSpecialist(SPECIALISTS_DREADNOUGHTS)) {
        col = obj_controller.bat_dreadnought_column;
        ftype = "dreadnought"; //dreadnoughts
        new_combat.dreadnoughts++;
    } else if (_unit_role == obj_ini.role[100][4]) {
        //terminators
        col = obj_controller.bat_terminator_column;
        ftype = "terminator";
        new_combat.terminators++;
    }

    if (moov > 0) {
        if (((moov == 1) && (obj_controller.command_set[8] == 1)) || ((moov == 2) && (obj_controller.command_set[9] == 1))) {
            if (company >= 2) {
                col = obj_controller.bat_tactical_column;
                ftype = "tactical";
            }
            if (company == 10) {
                col = obj_controller.bat_scout_column;
                ftype = "scout";
            }
            if (obj_ini.mobi[cooh][va] == "Jump Pack") {
                col = obj_controller.bat_assault_column;
                ftype = "assault";
            }
        }
    }

    if ((_unit_role == _role[15]) || (_unit_role == _role[14]) || unit.IsSpecialist(SPECIALISTS_TRAINEES)) {
        if (_unit_role == string(_role[14]) + " Aspirant") {
            col = obj_controller.bat_tactical_column;
            ftype = "tactical";
            new_combat.tacticals++;
        }

        if (_unit_role == _role[15]) {
            new_combat.apothecaries++;
        }
        if (_unit_role == _role[14]) {
            new_combat.chaplains++;
            if (new_combat.big_mofo > 5) {
                new_combat.big_mofo = 5;
            }
        }

        col = obj_controller.bat_tactical_column;
        ftype = "tactical";
        if (_wearing_armour) {
            if (_armour_data.has_tag("terminator")) {
                col = obj_controller.bat_terminator_column;
                ftype = "terminator";
            }
        }
        if (company == 10) {
            col = obj_controller.bat_scout_column;
            ftype = "scout";
        }
    }

    if ((_unit_role == _role[5]) || (_unit_role == _role[11]) || (_unit_role == _role[7])) {
        if (_unit_role == _role[5]) {
            new_combat.captains++;
            if (new_combat.big_mofo > 5) {
                new_combat.big_mofo = 5;
            }
        }
        if (_unit_role == _role[11]) {
            new_combat.standard_bearers++;
        }
        if (_unit_role == _role[7]) {
            new_combat.champions++;
        }
        if (company >= 2) {
            col = obj_controller.bat_tactical_column;
            ftype = "tactical";
        }
        if (company == 10) {
            col = obj_controller.bat_scout_column;
            ftype = "scout";
        }
        if (obj_ini.mobi[cooh][va] == "Jump Pack") {
            col = obj_controller.bat_assault_column;
            ftype = "assault";
        }
    }

    if (_unit_role == obj_ini.role[100][eROLE.CHAPTERMASTER]) {
        col = obj_controller.bat_command_column;
        ftype = "command";
        new_combat.important_dudes++;
        new_combat.big_mofo = 1;
        if (string_count("0", obj_ini.spe[cooh][va]) > 0) {
            new_combat.chapter_master_psyker = 1;
        } else {
            new_combat.chapter_master_psyker = 0;
        }
    }
    if (unit.IsSpecialist(SPECIALISTS_HEADS)) {
        col = obj_controller.bat_command_column;
        ftype = "command";
        new_combat.important_dudes++;
    }
    if (new_combat.big_mofo > 2) {
        new_combat.big_mofo = 2;
    }
    if (new_combat.big_mofo > 3) {
        new_combat.big_mofo = 3;
    }
    if (unit.squad != "none") {
        var squad = unit.get_squad();
        switch (squad.formation_place) {
            case "assault":
                col = obj_controller.bat_assault_column;
                ftype = "assault";
                break;
            case "veteran":
                col = obj_controller.bat_veteran_column;
                ftype = "veteran";
                break;
            case "tactical":
                col = obj_controller.bat_tactical_column;
                ftype = "tactical";
                break;
            case "devastator":
                col = obj_controller.bat_devastator_column;
                ftype = "devastator";
                break;
            case "terminator":
                col = obj_controller.bat_terminator_column;
                ftype = "terminator";
                break;
            case "command":
                col = obj_controller.bat_command_column;
                ftype = "command";
                break;
        }
    }
    if (col == 0) {
        col = obj_controller.bat_hire_column;
        ftype = "hire";
    }
    if (_unit_role == "Death Company") {
        // Ahahahahah
        var really = false;
        if (_wearing_armour) {
            really = _armour_data.has_tag("dreadnought");
        }

        if (!really) {
            new_combat.thirsty++;
        } else {
            new_combat.really_thirsty++;
        }
        col = max(obj_controller.bat_assault_column, obj_controller.bat_command_column, obj_controller.bat_honor_column, obj_controller.bat_dreadnought_column, obj_controller.bat_veteran_column);
        ftype = "deathco";
    }

    // ===== Guardsmen: "Hirelings" formation =====
    // Guardsmen go into the movable Hirelings block (bat_hire_column), so the player can position
    // them anywhere from the formation screen as a single line. This restores the behaviour from
    // before the positional-screen experiment: every guardsman shares this one column instead of
    // being pinned to fixed front columns. bat_hire_column was resolved for this formation at the
    // top of this function and is driven by the Hirelings bar (bat_hire_for, unit_id 12).
    if (_unit_role == "Heavy Weapons Team") {
        // Heavy weapons teams fight from the Devastator formation, the chapter's own heavy-weapon
        // line, instead of the Hirelings block, so the auxilia's heavy guns stand with the Marines'.
        col = obj_controller.bat_devastator_column;
        ftype = "devastator";
        new_combat.devastators++;
    } else if (_unit_role == "Guardsman" || _unit_role == "Guard Sergeant" || _unit_role == "Veteran Guard") {
        col = obj_controller.bat_hire_column;
        ftype = "hire";
    }

    targ = formation_block(ftype, col);

    with (targ) {
        scr_add_unit_to_roster(unit, is_local);
    }
}

function add_vehicle_to_battle(company, veh_index, is_local) {
    var new_combat = obj_ncombat;
    var v = veh_index;
    new_combat.veh_fighting[company][v] = 1;
    var col = 1, targ = 0;
    var ftype = "";

    switch (obj_ini.veh_role[company][v]) {
        case "Rhino":
            col = obj_controller.bat_rhino_column;
            ftype = "rhino";
            new_combat.rhinos++;
            break;
        // Chimera (guard transport) screens like a Rhino. This first drop parks it in the Rhino
        // column; the dedicated Imperial Armor column arrives with the formation drop.
        case "Chimera":
            col = obj_controller.bat_rhino_column;
            ftype = "rhino";
            new_combat.rhinos++;
            break;
        case "Predator":
            col = obj_controller.bat_predator_column;
            ftype = "predator";
            new_combat.predators++;
            break;
        // Leman Russ (guard battle tank) anchors the armour line alongside the Predators.
        case "Leman Russ":
            col = obj_controller.bat_predator_column;
            ftype = "predator";
            new_combat.predators++;
            break;
        case "Land Raider":
            col = obj_controller.bat_landraider_column;
            ftype = "landraider";
            new_combat.land_raiders++;
            break;
        case "Land Speeder":
            col = obj_controller.bat_landspeeder_column;
            ftype = "landspeeder";
            new_combat.land_speeders++;
            break;
        case "Whirlwind":
            col = obj_controller.bat_whirlwind_column;
            ftype = "whirlwind";
            new_combat.whirlwinds++;
            break;
    }

    /// @type {Asset.GMObject.obj_pnunit}
    targ = formation_block(ftype, col);
    targ.veh++;
    targ.veh_co[targ.veh] = company;
    targ.veh_id[targ.veh] = v;
    targ.veh_type[targ.veh] = obj_ini.veh_role[company][v];
    targ.veh_wep1[targ.veh] = obj_ini.veh_wep1[company][v];
    targ.veh_wep2[targ.veh] = obj_ini.veh_wep2[company][v];
    targ.veh_wep3[targ.veh] = obj_ini.veh_wep3[company][v];
    targ.veh_upgrade[targ.veh] = obj_ini.veh_upgrade[company][v];
    targ.veh_acc[targ.veh] = obj_ini.veh_acc[company][v];
    targ.veh_local[targ.veh] = is_local;

    if (obj_ini.veh_role[company][v] == "Land Speeder") {
        targ.veh_hp[targ.veh] = obj_ini.veh_hp[company][v] * 2.5;
        targ.veh_hp_multiplier[targ.veh] = 2.5;
        targ.veh_ac[targ.veh] = 25;
    } else if ((obj_ini.veh_role[company][v] == "Rhino") || (obj_ini.veh_role[company][v] == "Whirlwind")) {
        targ.veh_hp[targ.veh] = obj_ini.veh_hp[company][v] * 3;
        targ.veh_hp_multiplier[targ.veh] = 3;
        targ.veh_ac[targ.veh] = 35;
    } else if (obj_ini.veh_role[company][v] == "Predator") {
        targ.veh_hp[targ.veh] = obj_ini.veh_hp[company][v] * 3;
        targ.veh_hp_multiplier[targ.veh] = 3;
        targ.veh_ac[targ.veh] = 40;
    } else if (obj_ini.veh_role[company][v] == "Land Raider") {
        targ.veh_hp[targ.veh] = obj_ini.veh_hp[company][v] * 4;
        targ.veh_hp_multiplier[targ.veh] = 4;
        targ.veh_ac[targ.veh] = 40;
    } else if (obj_ini.veh_role[company][v] == "Chimera") {
        // Mirrors the enemy Chimera: HP 200 (base 100 x2), armour 30.
        targ.veh_hp[targ.veh] = obj_ini.veh_hp[company][v] * 2;
        targ.veh_hp_multiplier[targ.veh] = 2;
        targ.veh_ac[targ.veh] = 30;
    } else if (obj_ini.veh_role[company][v] == "Leman Russ") {
        // Mirrors the enemy Leman Russ Battle Tank: heavy armour 40, HP 300 (base 100 x3),
        // so it shrugs off small arms and trades blows with other tanks like a Predator.
        targ.veh_hp[targ.veh] = obj_ini.veh_hp[company][v] * 3;
        targ.veh_hp_multiplier[targ.veh] = 3;
        targ.veh_ac[targ.veh] = 40;
    } else if (obj_ini.veh_role[company][v] == "Basilisk") {
        // Mirrors the enemy Basilisk: armour 30, HP 150 (base 100 x1.5). A self-propelled
        // artillery piece, tougher than a Chimera but lighter than a Leman Russ, built to
        // shell from the rear of the line rather than trade blows at the front.
        targ.veh_hp[targ.veh] = obj_ini.veh_hp[company][v] * 1.5;
        targ.veh_hp_multiplier[targ.veh] = 1.5;
        targ.veh_ac[targ.veh] = 30;
    }

    // STC Bonuses
    if (targ.veh_type[targ.veh] != "") {
        if (obj_controller.stc_bonus[3] == 1) {
            targ.veh_hp[targ.veh] = round(targ.veh_hp[targ.veh] * 1.1);
            targ.veh_hp_multiplier[targ.veh] = targ.veh_hp_multiplier[targ.veh] * 1.1;
        }
        if (obj_controller.stc_bonus[3] == 2) {
            //TODO reimplement STC bonus for ranged vehicle weapons
            //veh ranged isn't a thing sooooo.... oh well
            //targ.veh_ranged[targ.veh] = targ.veh_ranged[targ.veh] * 1.05;
        }
        if (obj_controller.stc_bonus[3] == 5) {
            targ.veh_ac[targ.veh] = round(targ.veh_ac[targ.veh] * 1.1);
        }
        if (obj_controller.stc_bonus[4] == 1) {
            targ.veh_hp[targ.veh] = round(targ.veh_hp[targ.veh] * 1.1);
            targ.veh_hp_multiplier[targ.veh] = targ.veh_hp_multiplier[targ.veh] * 1.1;
        }
        if (obj_controller.stc_bonus[4] == 2) {
            targ.veh_ac[targ.veh] = round(targ.veh_ac[targ.veh] * 1.1);
        }
    }
}

/// @function auxilia_roles
/// @description Single source of truth for which unit roles belong to the Auxilia company
/// screen (managing 16). These are non-Astartes auxiliary mercenaries mustered into company 0
/// alongside the Headquarters, but managed on their own screen. Add future merc roles here
/// (Ogryn, heavy weapons team, etc.) and they will automatically appear under Auxilia and be
/// excluded from the Headquarters detail view.
/// @returns {array}
function auxilia_roles() {
    return ["Guardsman", "Guard Squad", "Guard Sergeant", "Veteran Guard", "Heavy Weapons Team"];
}

/// @description Promote one basic Guardsman to Veteran Guard: role swap plus the veteran
/// stat buff. Shared by promote_auxilia_to_veteran (veteranguard cheat, bulk path) and the
/// Auxilia screen Promote button (setup_promotion_popup, selection path). No XP gate here;
/// callers gate on GUARD_VETERAN_XP. The stat_boosts numbers are tunable; additions are
/// flat, and stat_boosts rebalances constitution into current health.
/// @param {Struct.TTRPG_stats} _unit  the Guardsman to promote
function promote_guardsman_to_veteran(_unit) {
    _unit.update_role("Veteran Guard");
    _unit.stat_boosts({
        ballistic_skill: 8,
        constitution: 6,
        dexterity: 4
    });
}

/// @description Promote every basic Guardsman to Veteran Guard, applying the veteran stat
/// buff. Veterans keep all Guard behaviour (Auxilia screen, hireling line, volley fire,
/// tenth-slot berth, guardsman portrait) through the role closure. They receive no free
/// weapon; Hellguns are forged separately and equip-gated to this role. The Auxilia screen
/// Promote button wires the per-selection path through promote_guardsman_to_veteran
/// (setup_promotion_popup); this bulk path serves the veteranguard cheat and future
/// promote-everything hooks. Pass a company index to limit promotion to one
/// company, or leave it undefined to promote every auxilia Guardsman. The stat_boosts numbers
/// are tunable. Additions are flat; stat_boosts rebalances constitution into current health.
/// @param {real} [_company]  optional company index to limit promotion to
/// @returns {real} number of troopers promoted
function promote_auxilia_to_veteran(_company = undefined) {
    var _troops = collect_role_group("all", "", false, { roles: ["Guardsman"] });
    var _count = 0;
    for (var _i = 0; _i < array_length(_troops); _i++) {
        var _unit = _troops[_i];
        if (_company != undefined && _unit.company != _company) {
            continue;
        }
        // Only battle-hardened Guardsmen qualify: they must have earned GUARD_VETERAN_XP
        // experience, roughly GUARD_VETERAN_XP / GUARD_BATTLE_XP survived battles. Fresh
        // recruits are skipped until they have bled for it.
        if (_unit.experience < GUARD_VETERAN_XP) {
            continue;
        }
        promote_guardsman_to_veteran(_unit);
        _count++;
    }
    return _count;
}
